;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Weather Notifications           ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2024) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                            Rule Include Section                         ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Tyre Information Retrieval.rules
#Include %kResourcesDirectory%Rules\Session Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                            Public Rules Section                         ;;;
;;;-------------------------------------------------------------------------;;;

priority: 5, {Any: [?Weather.Weather.10Min], {None: [?Weather.Weather.10Min.Index]}} => (Prove: updateWeatherIndex(10Min))
priority: 5, {Any: [?Weather.Weather.30Min], {None: [?Weather.Weather.30Min.Index]}} => (Prove: updateWeatherIndex(30Min))

updateWeatherIndex(?time) <= Get(Weather.Weather, ?time, ?weather), weatherIndex(?weather, ?index), Set(Weather.Weather, ?time, Index, ?index)

priority: 2,
	{All: [?Lap], [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]}} =>
		(Set: Weather.Tyre.Compound.Target, Dry), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Intermediate, ?)},
		  {Any: [?Weather.Weather.10Min = Drizzle], [?Weather.Weather.10Min = LightRain]}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Intermediate))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Wet, ?)},
		  [?Weather.Weather.10Min = LightRain], {None: {Prove: availableTyreCompound(Intermediate, ?)}}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Wet))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Dry], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Wet, ?)},
		  {Any: [?Weather.Weather.10Min = MediumRain], [?Weather.Weather.10Min = HeavyRain], [?Weather.Weather.10Min = Thunderstorm]}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Wet))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Dry],
		  {Any: [?Weather.Weather.10Min = Dry],
				{All: [?Weather.Weather.10Min = Drizzle], {None: {Prove: availableTyreCompound(Intermediate, ?)}}},
				{All: {None: {Prove: availableTyreCompound(Intermediate, ?)}}, {None: {Prove: availableTyreCompound(Wet, ?)}}}}} =>
			(Set: Weather.Tyre.Compound.Target, Dry), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)

priority: 2,
	{All: [?Lap], [?Tyre.Compound = Intermediate], {None: [?Weather.Tyre.Change.Recommended]}} =>
		(Set: Weather.Tyre.Compound.Target, Intermediate), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)		
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Intermediate], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Dry, ?)}, [?Weather.Weather.10Min = Dry]} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Dry))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Intermediate], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Wet, ?)},
		  {Any: [?Weather.Weather.10Min = MediumRain], [?Weather.Weather.10Min = HeavyRain], [?Weather.Weather.10Min = Thunderstorm]}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Wet))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Intermediate],
		  {Any: [?Weather.Weather.10Min = Drizzle], [?Weather.Weather.10Min = LightRain],
				{All: [?Weather.Weather.10Min = Dry], {None: {Prove: availableTyreCompound(Dry, ?)}}},
				{All: {Any: [?Weather.Weather.10Min = MediumRain], [?Weather.Weather.10Min = HeavyRain], [?Weather.Weather.10Min = Thunderstorm]},
					  {None: {Prove: availableTyreCompound(Wet, ?)}}}}} =>
			(Set: Weather.Tyre.Compound.Target, Intermediate), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)

priority: 2,
	{All: [?Lap], [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]}} =>
		(Set: Weather.Tyre.Compound.Target, Wet), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)		
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Dry, ?)}, [?Weather.Weather.10Min = Dry]} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Dry))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]},
		  {Prove: availableTyreCompound(Intermediate, ?)},
		  {Any: [?Weather.Weather.10Min = Drizzle], [?Weather.Weather.10Min = LightRain]}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Intermediate))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Wet], {None: [?Weather.Tyre.Change.Recommended]},
		  {None: {Prove: availableTyreCompound(Intermediate, ?)}}, {Any: [?Weather.Weather.10Min = Drizzle]}} =>
		(Prove: weatherTyreChangeRecommendation(?Lap, ?Weather.Weather.10Min, 10, Dry))
priority: 1,
	{All: [?Lap], [?Tyre.Compound = Wet],
		  {Any: [?Weather.Weather.10Min = MediumRain], [?Weather.Weather.10Min = HeavyRain], [?Weather.Weather.10Min = Thunderstorm],
		        {All: {None: {Prove: availableTyreCompound(Intermediate, ?)}}, [?Weather.Weather.10Min = LightRain]},
				{All: {None: {Prove: availableTyreCompound(Dry, ?)}}, {None: {Prove: availableTyreCompound(Intermediate, ?)}}}}} =>
		(Set: Weather.Tyre.Compound.Target, Wet), (Set: Weather.Tyre.Compound.Color.Target, !Tyre.Compound.Color)
		
{All: [?Lap > 1], [?Tyre.Compound = Dry],
	  {Any: {All: {None: [?Weather.Change.Notified]},
				  {Any: [?Weather.Weather.Now = Dry], [?Weather.Weather.Now = Drizzle]},
				  {Any: [?Weather.Weather.30Min = LightRain], [?Weather.Weather.30Min = MediumRain],
						[?Weather.Weather.30Min = HeavyRain], [?Weather.Weather.30Min = Thunderstorm]}},
			[?Weather.Change.Notified < ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherChangeNotification(?Lap, !Weather.Weather.30Min, 30))
{All: [?Lap > 1], [?Tyre.Compound = Intermediate],
	  {Any: {All: {None: [?Weather.Change.Notified]},
				  {Any: [?Weather.Weather.Now = Drizzle], [?Weather.Weather.Now = LightRain]},
				  {Any: [?Weather.Weather.30Min = Dry], [?Weather.Weather.30Min = MediumRain],
						[?Weather.Weather.30Min = HeavyRain], [?Weather.Weather.30Min = Thunderstorm]}},
			[?Weather.Change.Notified > ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherChangeNotification(?Lap, !Weather.Weather.30Min, 30))
{All: [?Lap > 1], [?Tyre.Compound = Wet],
	  {Any: {All: {None: [?Weather.Change.Notified]},
				  {Any: [?Weather.Weather.Now = LightRain], [?Weather.Weather.Now = MediumRain],
						[?Weather.Weather.Now = HeavyRain], [?Weather.Weather.Now = Thunderstorm]},
				  {Any: [?Weather.Weather.30Min = Dry], [?Weather.Weather.30Min = Drizzle]}},
			[?Weather.Change.Notified > ?Weather.Weather.30Min.Index]}} =>
		(Prove: weatherChangeNotification(?Lap, !Weather.Weather.30Min, 30))
		
updateWeatherTyreCompoundColorTarget(?compound) <= availableTyreCompounds(?compound, ?compoundColor), Set(Weather.Tyre.Compound.Color.Target, ?compoundColor)

priority: -10, [?Pitstop.Lap = ?Lap] => (Prove: clearWeatherTyreChangeNotification(?Lap)),
										(Clear: Weather.Tyre.Compound.Target), (Clear: Weather.Tyre.Compound.Color.Target)

clearWeatherTyreChangeNotification(?lap) <= ?clearLap = ?lap + 5, Set(Weather.Tyre.Change.Clear, ?clearLap)

priority: -10, [?Weather.Tyre.Change.Clear = ?Lap] => (Clear: Weather.Tyre.Change.Recommended)

weatherChangeNotification(?lap, ?weather, ?minutes) <= remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000,
													   ?remainingMinutes > ?minutes, Call(weatherChangeNotification, true, ?minutes),
													   weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)
weatherChangeNotification(?lap, ?weather, ?minutes) <= remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000,
													   =<(?remainingMinutes, ?minutes), Call(weatherChangeNotification, false, ?minutes),
													   weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)
											 
weatherTyreChangeRecommendation(?lap, ?weather, ?minutes, ?compound) <=
		remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000, ?remainingMinutes > ?minutes,
		Call(weatherTyreChangeRecommendation, ?minutes, ?compound),
		updateWeatherNotified(?compound, ?weather), updateTyreChangeRecommended(?lap, ?weather),
		Set(Weather.Tyre.Compound.Target, ?compound), tyreCompoundColor(?compound, ?color), Set(Weather.Tyre.Compound.Color.Target, ?color)
weatherTyreChangeRecommendation(?lap, ?weather, ?minutes, ?compound) <=
		remainingStintTime(?lap, ?stintTime), ?remainingMinutes = ?stintTime / 60000, =<(?remainingMinutes, ?minutes),
		Call(weatherChangeNotification, false, ?minutes),
		updateWeatherNotified(?compound, ?weather), updateTyreChangeRecommended(?lap, ?weather)

updateTyreChangeRecommended(?lap, ?weather) <= ?lap > 1, !, weatherIndex(?weather, ?index), Set(Weather.Tyre.Change.Recommended, ?index)
updateTyreChangeRecommended(?, ?)

updateWeatherNotified(?, ?weather) <=
		unbound?(!Weather.Change.Notified), !, weatherIndex(?weather, ?index), Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Dry, ?weather) <=
		weatherIndex(?weather, ?index), ?index < !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Intermediate, ?weather) <=
		weatherIndex(?weather, ?index), ?index > !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(Wet, ?weather) <=
		weatherIndex(?weather, ?index), ?index > !Weather.Change.Notified, !, Set(Weather.Change.Notified, ?index)
updateWeatherNotified(?, ?)
