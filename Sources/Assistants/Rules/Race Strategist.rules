;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Race Strategist Rules           ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2021) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                             Global Trigger                              ;;;
;;;                                                                         ;;;
;;; Entry points for the surrounding script code. Typically, the rule       ;;;
;;; will be triggered by setting a target fact and call "produce()" on the  ;;;
;;; KnowledgeBase instance.                                                 ;;;
;;;-------------------------------------------------------------------------;;;

;;;-------------------------------------------------------------------------;;;
;;; Each lap, after all the lap data has been copied to the knowledge base, ;;;
;;; all target data for race positions, lap times and strategy will be      ;;;
;;; recomputed upon the data of the last laps, and so on.                   ;;;
;;;-------------------------------------------------------------------------;;;

; [?Lap] => ...


;;;-------------------------------------------------------------------------;;;
;;; You can set this fact to a future lap number to request a standings     ;;;
;;; projection for this future lap.                                         ;;;
;;;-------------------------------------------------------------------------;;;

; [?Standings.Extrapolate] => ...


;;;-------------------------------------------------------------------------;;;
;;; "Pitstop.Strategy.Plan" can be set, if you want to run an analysis for  ;;;
;;; the best lap for the next pitstop. If this fact is set to true, the     ;;;
;;; analysis searches for a lap using the remaining fuel as a hint for the  ;;;
;;; window of laps to analyze. But you can also set "Pitstop.Strategy.Plan" ;;;
;;; to a specific lap, if you want to search for the best pitstop option in ;;;
;;; a lap close to this given lap.                                          ;;;
;;;-------------------------------------------------------------------------;;;

; [?Pitstop.Strategy.Plan] => ...


;;;-------------------------------------------------------------------------;;;
;;; "Pitstop.Lap" must be set to the lap number, where the pitstop has been ;;;
;;; done.                                                                   ;;;
;;;-------------------------------------------------------------------------;;;

; [?Pitstop.Lap] => ...


;;;-------------------------------------------------------------------------;;;
;;;                          Lap & Fuel Calculation                         ;;;
;;;                                                                         ;;;
;;; Updates the remaining laps with the current amount of fuel and the      ;;;
;;; remaining stint time for the current driver.                            ;;;
;;;-------------------------------------------------------------------------;;;

priority: 20, [?Lap] => (ProveAll: updateRemainingLaps, ?Lap)

updateRemainingLaps(?lap) <= remainingStintLaps(?lap, Fuel, ?fuelLaps), Set(Lap.Remaining.Fuel, ?fuelLaps)
updateRemainingLaps(?lap) <= remainingStintLaps(?lap, Driver, ?driverLaps), Set(Lap.Remaining.Stint, ?driverLaps)
updateRemainingLaps(?lap) <= remainingSessionLaps(?lap, ?sessionLaps), Set(Lap.Remaining.Session, ?sessionLaps)
updateRemainingLaps(?lap) <= !Lap.Remaining.Stint > !Lap.Remaining.Fuel, Set(Lap.Remaining.Stint, !Lap.Remaining.Fuel)
updateRemainingLaps(?lap) <= !Lap.Remaining.Stint > !Lap.Remaining.Session, Set(Lap.Remaining.Stint, !Lap.Remaining.Session)

priority: -10, {All: [?Lap.Remaining.Stint <= ?Lap.Remaining.Fuel],																				\
					 [?Lap.Remaining.Stint <= ?Lap.Remaining.Session]} => (Set: Lap.Remaining, ?Lap.Remaining.Stint)
priority: -10, {All: [?Lap.Remaining.Session <= ?Lap.Remaining.Fuel],																			\
					 [?Lap.Remaining.Session <= ?Lap.Remaining.Stint]} => (Set: Lap.Remaining, ?Lap.Remaining.Session)
priority: -10, {All: [?Lap.Remaining.Fuel <= ?Lap.Remaining.Stint],																				\
					 [?Lap.Remaining.Fuel <= ?Lap.Remaining.Session]} => (Set: Lap.Remaining, ?Lap.Remaining.Fuel)


;;;-------------------------------------------------------------------------;;;
;;;                Weather & Tyre Temperature Trend Calculation             ;;;
;;;                                                                         ;;;
;;; The rules in this section try to derive future weather data on past     ;;;
;;; weather trends and weather forecast information (as far as available).  ;;;
;;;-------------------------------------------------------------------------;;;

priority: 5, {Any: [?Weather.Weather.10Min], {None: [?Weather.Weather.10Min.Index]}} => (Prove: updateWeatherIndex, 10Min)
priority: 5, {Any: [?Weather.Weather.30Min], {None: [?Weather.Weather.30Min.Index]}} => (Prove: updateWeatherIndex, 30Min)

updateWeatherIndex(?time) <= Get(Weather.Weather, ?time, ?weather), weatherIndex(?weather, ?index), Set(Weather.Weather, ?time, Index, ?index)

[?Lap > 5] => (Prove: updateWeatherTrend, ?Lap)

updateWeatherTrend(?lap) <= firstStintLap(?startlap), ?deltaLaps = ?lap - ?startlap,															\
							lapAirTemperature(?startlap, ?startAirTemperature), projectedAirTemperature(?lap, ?curAirTemperature),				\
							lapTrackTemperature(?startlap, ?startTrackTemperature), projectedTrackTemperature(?lap, ?curTrackTemperature),		\
							lapWeather(?startlap, ?startWeather), projectedWeather(?lap, ?curWeather),											\
							lapTyreTemperature(?startlap, ?startTyreTemperature), projectedTyreTemperature(?lap, ?curTyreTemperature),			\
							?tempAir = ?curAirTemperature - ?startAirTemperature, ?tempTrack = ?curTrackTemperature - ?startTrackTemperature,	\
							?deltaAirTemperature = ?tempAir / ?deltaLaps, ?deltaTrackTemperature = ?tempTrack / ?deltaLaps,						\
							?tempWeather = ?curWeather - ?startWeather, ?deltaWeather = ?tempWeather / ?deltaLaps,								\
							?tempTyreTemperature = ?curTyreTemperature - ?startTyreTemperature,													\
							?deltaTyreTemperature = ?tempTyreTemperature / ?deltaLaps,															\
							Set(Weather.Temperature.Air.Delta, ?deltaAirTemperature),															\
							Set(Weather.Temperature.Track.Delta, ?deltaTrackTemperature), Set(Weather.Weather.Delta, ?deltaWeather),			\
							Set(Weather.Temperature.Tyre.Delta, ?deltaTyreTemperature)

projectedWeather(?lap, ?weather) <=																												\
		firstStintLap(?firstlap), sequence(?firstlap, !Lap, ?indices), lapWeathers(?firstlap, !Lap, ?weathers),									\
		linRegression(?indices, ?weathers, ?a, ?b), ?temp = ?b * ?lap, ?weather = ?a + ?temp

projectedAirTemperature(?lap, ?temperature) <=																									\
		firstStintLap(?firstlap), sequence(?firstlap, !Lap, ?indices), lapAirTemperatures(?firstlap, !Lap, ?temperatures),						\
		linRegression(?indices, ?temperatures, ?a, ?b), ?temp = ?b * ?lap, ?temperature = ?a + ?temp

projectedTrackTemperature(?lap, ?temperature) <=																								\
		firstStintLap(?firstlap), sequence(?firstlap, !Lap, ?indices), lapTrackTemperatures(?firstlap, !Lap, ?temperatures),					\
		linRegression(?indices, ?temperatures, ?a, ?b), ?temp = ?b * ?lap, ?temperature = ?a + ?temp

projectedTyreTemperature(?lap, ?temperature) <=																									\
		firstStintLap(?firstlap), sequence(?firstlap, !Lap, ?indices), lapTyreTemperatures(?firstlap, !Lap, ?temperatures),						\
		linRegression(?indices, ?temperatures, ?a, ?b), ?temp = ?b * ?lap, ?temperature = ?a + ?temp


;;;-------------------------------------------------------------------------;;;
;;;                                  Lap Time Calculation                   ;;;
;;;                                                                         ;;;
;;; Updates the for all cars each lap.                                      ;;;
;;;-------------------------------------------------------------------------;;;

{Any: [?Lap], [?Sector]} => (Prove: updateLapTimes, ?Lap, !Car.Count)

updateLapTimes(?, ?car) <= =<(?car, 0), !
updateLapTimes(?lap, ?car) <= carLapTime(?car, ?lapTime), Set(Standings.Lap, ?lap, Car, ?car, Time, ?lapTime),									\
							  ?nextCar = ?car - 1, updateLapTimes(?lap, ?nextCar)


;;;-------------------------------------------------------------------------;;;
;;;                      Position & Lap Delta Calculation                   ;;;
;;;                                                                         ;;;
;;; Updates the gaps and lap time deltas for various cars each lap.         ;;;
;;;-------------------------------------------------------------------------;;;

priority: 5, {Any: [?Lap], [?Sector]} => (Clear: Position.Front.Car), (Clear: Position.Front.Distance), (Clear: Position.Front.Delta),			\
										 (Clear: Position.Behind.Car), (Clear: Position.Behind.Distance), (Clear: Position.Behind.Delta),		\
										 (Clear: Position.Leader.Car), (Clear: Position.Leader.Distance), (Clear: Position.Leader.Delta)
{Any: [?Lap], [?Sector]} => (Prove: updateStandings, ?Lap)
priority: -5, [?Sector] => (Clear: Sector)

updateStandings(?lap) <= updatePosition(?lap, !Car.Count)

updatePosition(?, ?car) <= =<(?car, 0), !
updatePosition(?lap, !Driver.Car) <= !, carPosition(!Driver.Car, ?position), Set(Position, ?position), carLaps(!Driver.Car, FULL, ?laps),		\
									 Set(Standings.Lap, ?lap, Position, ?position), Set(Standings.Lap, ?lap, Car, !Driver.Car, Delta, 0),		\
									 Set(Standings.Lap, ?lap, Car, !Driver.Car, Laps, ?laps),													\
									 Set(Standings.Lap, ?lap, Car, !Driver.Car, Position, ?position),											\
									 ?nextCar = !Driver.Car - 1, updatePosition(?lap, ?nextCar)
updatePosition(?lap, ?car) <= carPosition(?car, ?position), carLaps(?car, FULL, ?carLaps), carLaps(!Driver.Car, FULL, ?driverLaps),				\
							  lapTime(?lap, ?lapTime), ?distance = ?driverLaps - ?carLaps, ?delta = ?lapTime * ?distance,						\
							  Set(Standings.Lap, ?lap, Car, ?car, Delta, ?delta),																\
							  Set(Standings.Lap, ?lap, Car, ?car, Laps, ?carLaps),																\
							  Set(Standings.Lap, ?lap, Car, ?car, Position, ?position), 														\
							  updateFrontDelta(?lap, ?car, ?distance, ?delta), updateBehindDelta(?lap, ?car, ?distance, ?delta),				\
							  updateLeadDelta(?lap, ?car, ?distance, ?delta), ?nextCar = ?car - 1, updatePosition(?lap, ?nextCar)

updateFrontDelta(?, ?car, ?distance, ?delta) <= ?delta < 0, unbound?(!Position.Front.Car), !, Set(Position.Front.Car, ?car),					\
												Set(Position.Front.Distance, ?distance), Set(Position.Front.Delta, ?delta)
updateFrontDelta(?, ?car, ?distance, ?delta) <= ?delta < 0, Get(Position.Front.Delta, ?curDelta), ?delta > ?curDelta,							\
												Set(Position.Front.Car, ?car), Set(Position.Front.Distance, ?distance),							\
												Set(Position.Front.Delta, ?delta)
updateFrontDelta(?, ?, ?, ?)

updateBehindDelta(?, ?car, ?distance, ?delta) <= ?delta > 0, unbound?(!Position.Behind.Car), !, Set(Position.Behind.Car, ?car),					\
												 Set(Position.Behind.Distance, ?distance), Set(Position.Behind.Delta, ?delta)
updateBehindDelta(?, ?car, ?distance, ?delta) <= ?delta > 0, Get(Position.Behind.Delta, ?curDelta), ?delta < ?curDelta,							\
												 Set(Position.Behind.Car, ?car), Set(Position.Behind.Distance, ?distance),						\
												 Set(Position.Behind.Delta, ?delta)
updateBehindDelta(?, ?, ?, ?)

updateLeadDelta(?lap, ?car, ?distance, ?delta) <= carPosition(?car, 1), !, Set(Position.Leader.Car, ?car),										\
												  Set(Position.Leader.Distance, ?distance), Set(Position.Leader.Delta, ?delta)
updateLeadDelta(?, ?, ?, ?)


;;;-------------------------------------------------------------------------;;;
;;;                            Position Projection                          ;;;
;;;                                                                         ;;;
;;; This rule set will extrapolate the race positions into the future.      ;;;
;;;-------------------------------------------------------------------------;;;
		
[?Standings.Extrapolate] => (Prove: extrapolateLap, !Lap, ?Standings.Extrapolate), (Clear: Standings.Extrapolate)

extrapolateLap(?lap, ?futureLap) <= ?futureLap < ?lap, !, fail
extrapolateLap(?lap, ?futureLap) <= extrapolateLap(?lap, ?futureLap, 0)

extrapolateLap(?lap, ?futureLap, ?pitDelta) <=																									\
		carAvgLapTime(?lap, !Driver.Car, ?avgLapTime), ?lapDelta = ?futureLap - ?lap, 															\
		?futureTime = ?lapDelta * ?avgLapTime, extrapolatePositions(?lap, !Car.Count, ?futureLap, ?futureTime, ?pitDelta),						\
		Call(updatePositions, ?futureLap),																										\
		correctPositions(?lap, !Car.Count, !Session.Settings.Standings.Extrapolation.Overtake.Delta, ?futureLap),								\
		Call(updatePositions, ?futureLap)
		
extrapolateLaps(?lap, ?lapDelta) <= =<(?lapDelta, 0), !
extrapolateLaps(?lap, ?lapDelta) <= ?futureLap = ?lap + ?lapDelta, extrapolateLap(?lap, ?futureLap),											\
									?nLapDelta = ?lapDelta - 1, extrapolateLaps(?lap, ?nLapDelta)

priority: -20, [?Lap] => (Prove: clearExtrapolatedLap, ?Lap, !Car.Count)

clearExtrapolatedLap(?, ?car) <= =<(?car, 0), !
clearExtrapolatedLap(?lap, ?car)  <= Clear(Standings.Extrapolated, ?lap, Car, ?car, Position),													\
									 Clear(Standings.Extrapolated, ?lap, Car, ?car, Laps),														\
									 ?nextCar = ?car - 1, clearExtrapolatedLap(?lap, ?nextCar)

priority: -20, {All: [?Lap], [?Session.Settings.Standings.Extrapolation.Laps]} =>																\
		(Prove: extrapolatePositions, ?Lap, ?Session.Settings.Standings.Extrapolation.Laps),													\
		(Prove: updatePositions, ?Lap, ?Session.Settings.Standings.Extrapolation.Laps),															\
		(Prove: correctPositions, ?Lap, !Session.Settings.Standings.Extrapolation.Overtake.Delta,												\
										?Session.Settings.Standings.Extrapolation.Laps),														\
		(Prove: updatePositions, ?Lap, ?Session.Settings.Standings.Extrapolation.Laps)
		
extrapolatePositions(?, ?lapDelta) <= =<(?lapDelta, 0), !
extrapolatePositions(?lap, ?lapDelta) <= carAvgLapTime(?lap, !Driver.Car, ?avgLapTime), ?futureTime = ?lapDelta * ?avgLapTime,					\
										 ?futureLap = ?lap + ?lapDelta, extrapolatePositions(?lap, !Car.Count, ?futureLap, ?futureTime, 0),		\
										 ?nextDelta = ?lapDelta - 1, extrapolatePositions(?lap, ?nextDelta)

extrapolatePositions(?, ?car, ?, ?, ?) <= =<(?car, 0), !
extrapolatePositions(?, ?, ?, 0, ?) <= !
extrapolatePositions(?lap, ?car, ?futureLap, ?deltaTime, ?) <= carAvgLapTime(?lap, ?car, 0), !
extrapolatePositions(?lap, !Driver.Car, ?futureLap, ?deltaTime, ?deltaPitTime) <=																\
		?delta != 0, !, extrapolatePosition(?lap, !Driver.Car, ?futureLap, ?deltaTime, 0),														\
		?nextCar = !Driver.Car - 1, extrapolatePositions(?lap, ?nextCar, ?futureLap, ?deltaTime, ?deltaPitTime)
extrapolatePositions(?lap, ?car, ?futureLap, ?deltaTime, ?deltaPitTime) <=																		\
		extrapolatePosition(?lap, ?car, ?futureLap, ?deltaTime, ?deltaPitTime),																	\
		?nextCar = ?car - 1, extrapolatePositions(?lap, ?nextCar, ?futureLap, ?deltaTime, ?deltaPitTime)

extrapolatePosition(?lap, ?car, ?futureLap, ?deltaTime, ?deltaPitTime) <=																		\
		?sumDeltaTime = ?deltaTime + ?deltaPitTime,																								\
		carAvgLapTime(?lap, ?car, ?carLapTime), ?deltaLaps = ?sumDeltaTime / ?carLapTime,														\
		Get(Standings.Lap, ?lap, Car, ?car, Laps, ?carLaps), ?futureLaps = ?carLaps + ?deltaLaps,												\
		Set(Standings.Extrapolated, ?futureLap, Car, ?car, Laps, ?futureLaps)
		
updatePositions(?, ?lapDelta) <= =<(?lapDelta, 0), !
updatePositions(?lap, ?lapDelta) <= ?futureLap = ?lap + ?lapDelta, Call(updatePositions, ?futureLap),											\
									?nextDelta = ?lapDelta - 1, updatePositions(?lap, ?nextDelta)

correctPositions(?, ?, ?lapDelta) <= =<(?lapDelta, 0), !
correctPositions(?lap, ?overtakeDelta, ?lapDelta) <= ?futureLap = ?lap + ?lapDelta,																\
													 correctPositions(?lap, !Car.Count, ?overtakeDelta, ?futureLap),							\
													 ?nextDelta = ?lapDelta - 1, correctPositions(?lap, ?overtakeDelta, ?nextDelta)

correctPositions(?, ?car, ?, ?) <= =<(?car, 0), !
correctPositions(?lap, ?car, ?overtakeDelta, ?futureLap) <= Get(Standings.Lap, ?lap, Car, ?car, Position, ?currentPosition),					\
															Get(Standings.Extrapolated, ?futureLap, Car, ?car, Position, ?futurePosition),		\
															?deltaPosition = ?futurePosition - ?currentPosition,								\
															?temp = ?overtakeDelta * ?deltaPosition, abs(?temp, ?correction),					\
															correctPosition(?lap, ?car, ?correction, ?futureLap),								\
															?nextCar = ?car - 1, correctPositions(?lap, ?nextCar, ?overtakeDelta, ?futureLap)

correctPosition(?, ?, 0, ?) <= !
correctPosition(?lap, ?car, ?timeCorrection, ?futureLap) <= carAvgLapTime(?lap, ?car, 0), !  
correctPosition(?lap, ?car, ?timeCorrection, ?futureLap) <= carAvgLapTime(?lap, ?car, ?carLapTime), ?deltaLaps = ?timeCorrection / ?carLapTime,	\
															Get(Standings.Extrapolated, ?futureLap, Car, ?car, Laps, ?fullLaps),				\
															?correctedLaps = ?fullLaps - ?deltaLaps,											\
															Set(Standings.Extrapolated, ?futureLap, Car, ?car, Laps, ?correctedLaps)

carAvgLapTime(?lap, ?car, ?avgTime) <= Get(Standings.Lap, ?lap, Car, ?car, Time.Average, ?avgTime)
carAvgLapTime(?lap, ?car, ?avgTime) <= carWeightedLapTimes(?lap, ?car, ?lapTimes), carLapAvgTime(?lap, ?car, ?lapTimes, ?avgTime),				\
									   Set(Standings.Lap, ?lap, Car, ?car, Time.Average, ?avgTime)

carWeightedLapTimes(?lap, ?car, ?times) <= carWeightedLapTimes(?lap, ?car, 0, ?temp), remove(?temp, 0, ?times)

carWeightedLapTimes(?lap, ?, ?lap, []) <= !
carWeightedLapTimes(?, ?, ?offset, []) <= >=(?offset, !Session.Settings.Lap.History.Considered), !
carWeightedLapTimes(?lap, ?, ?offset, [0]) <= ?tLap = ?lap - ?offset, ?tLap = 1
carWeightedLapTimes(?lap, ?car, ?offset, [?time | ?times]) <= ?tLap = ?lap - ?offset, carWeightedLapTime(?lap, ?car, ?tlap, ?time),				\
															  ?nOffset = ?offset + 1, carWeightedLapTimes(?lap, ?car, ?nOffset, ?times)

carWeightedLapTime(?referenceLap, ?car, ?lap, ?time) <=																							\
		carLapTime(?referenceLap, ?car, ?lastTime), carLapTime(?lap, ?car, ?lapTime),															\
		lapWeight(?referenceLap, ?lap, ?weight), ?delta = ?lastTime - ?lapTime, ?weightedDelta = ?delta * ?weight,								\
		?time = ?lastTime - ?weightedDelta

carLapTime(?lap, ?, 0) <= =<(?lap, 0), !
carLapTime(?lap, ?car, ?lapTime) <= Get(Standings.Lap, ?lap, Car, ?car, Time, ?lapTime), !
carLapTime(?lap, ?car, ?lapTime) <= ?pLap = ?lap - 1, carLapTime(?pLap, ?car, ?lapTime)

carLapAvgTime(?lap, !Driver.Car, ?lapTimes, ?avgTime) <= !, lapAvgTime(?lap, ?lapTimes, ?avgTime)
carLapAvgTime(?lap, ?, [], 0) <= !
carLapAvgTime(?lap, ?car, ?times, ?avgTime) <= sum(?times, ?sTimes), length(?times, ?sLength), ?avgTime = ?sTimes / ?sLength


;;;-------------------------------------------------------------------------;;;
;;;                           Pit Strategy Planning                         ;;;
;;;                                                                         ;;;
;;; This rule set will compute the best possible pitstop strategy.          ;;;
;;;-------------------------------------------------------------------------;;;

priority: 5, [?Pitstop.Strategy.Plan] => (Clear: Pitstop.Strategy.Lap)

[?Pitstop.Strategy.Plan = true] => (Prove: updatePitstopStrategy, !Lap)
[?Pitstop.Strategy.Plan > 1] => (Prove: updatePitstopStrategy, !Pitstop.Strategy.Plan)

priority: -5, [?Pitstop.Strategy.Plan] => (Clear: Pitstop.Strategy.Plan)

updatePitstopStrategy(?lap) <= ?lap < !Lap, !, fail
updatePitstopStrategy(!Lap) <= !, ?temp = !Lap.Remaining.Fuel / 2, round(?temp, ?halfFuelLap),													\
							   ?targetLap = !Lap + ?halfFuelLap, ?targetLap != !Lap, updatePitstopStrategy(?targetLap)
updatePitstopStrategy(?lap) <= ?deltaLaps = ?lap - !Lap, ?deltaFuelLaps = !Lap.Remaining.Fuel - ?deltaLaps, ?deltaFuelLaps > 0,					\
							   updatePitstopStrategy(?lap, ?deltaFuelLaps)

updatePitstopStrategy(?targetLap, ?remainingLaps) <= ?remainingLaps < 3, Set(Pitstop.Strategy.Lap, ?targetLap), !
updatePitstopStrategy(?targetLap, ?remainingLaps) <= pitstopWindow(!Lap, ?targetLap, ?remainingLaps, ?fromLap, ?toLap), >=(?toLap, ?fromLap),	\
													 updatePitstopStrategy(!Lap, ?fromLap, ?toLap)

updatePitstopStrategy(?lap, ?fromLap, ?fromLap) <= !, Set(Pitstop.Strategy.Lap, ?fromLap)
updatePitstopStrategy(?lap, ?fromLap, ?toLap) <= updatePitstopStrategy(?lap, ?fromLap, ?toLap, ?laps, ?positions, ?trafficDensities),			\
												 Call(msgbox, ?laps, ?positions, ?trafficDensities), bestPitstopLap(?laps, ?positions, ?trafficDensities, ?lap), Set(Pitstop.Strategy.Lap, ?lap)

updatePitstopStrategy(?lap, ?fromLap, ?fromLap, [], [], []) <= !
updatePitstopStrategy(?lap, ?fromLap, ?toLap, [?fromLap | ?laps], [?position | ?positions], [?trafficDensity | ?trafficDensities]) <=			\
		pitstopDeltaTime(?lap, ?fromLap, ?pitDelta), extrapolateLap(?lap, ?fromLap, ?pitDelta),													\
		evaluatePosition(?fromLap, ?position, ?trafficDensity), 																				\
		?nextLap = ?fromLap + 1, updatePitstopStrategy(?lap, ?nextLap, ?toLap, ?laps, ?positions, ?trafficDensities)

pitstopWindow(?lap, ?targetLap, ?remainingLaps, ?fromLap, ?toLap) <=																			\
		?maxLap = ?targetLap + ?remainingLaps,																									\
		?tempSL = ?targetLap - !Session.Settings.Pitstop.Strategy.Window.Considered, max(?lap, ?tempSL, ?fromLap),								\
		?tempTL = ?targetLap + !Session.Settings.Pitstop.Strategy.Window.Considered, min(?maxLap, ?tempTL, ?toLap)

pitstopDeltaTime(?lap, ?targetLap, ?pitstopDeltaTime) <= pitstopRefuelServiceTime(?lap, ?targetLap, ?refuelServiceTime), 						\
														 pitstopTyreServiceTime(?lap, ?targetLap, ?tyreServiceTime),							\
														 ?temp1 = ?refuelServiceTime + ?tyreServiceTime,										\
														 ?temp2 = !Session.Settings.Pitstop.Delta + ?temp1, ?pitstopDeltaTime = ?temp2 * 1000

pitstopRefuelServiceTime(?lap, ?targetLap, ?refillTime) <=																						\
		?deltaLaps = ?targetLap - ?lap,																											\
		lapAvgFuel(?lap, ?avgFuel), lapRemainingFuel(?lap, ?temp1), ?correction = ?deltaLaps * ?avgFuel, ?remainingFuel = ?temp1 - ?correction,	\
		remainingSessionLaps(?lap, ?tempLaps), ?remainingLaps = ?tempLaps - ?deltaLaps,															\
		?sessionFuel = ?avgFuel * ?remainingLaps, safetyFuel(?avgFuel, ?safetyFuel),															\
		?neededFuel = ?sessionFuel + ?safetyFuel, ?refillAmount = ?neededFuel - ?remainingFuel,													\
		min(?refillAmount, !Session.Settings.Fuel.Max, ?temp2), max(0, ?temp2, ?adjustedRefillAmount),											\
		?refillIncrements = ?adjustedRefillAmount / 10, ?refillTime = ?refillIncrements * !Session.Settings.Pitstop.Service.Refuel

pitstopTyreServiceTime(?lap, ?targetLap, !Session.Settings.Pitstop.Service.Tyres) <=															\
		?deltaLaps = ?targetLap - ?lap,																											\
		remainingSessionLaps(?lap, ?tempLaps), ?remainingLaps = ?tempLaps - ?deltaLaps,															\
		?remainingLaps > 5, ! 
pitstopTyreServiceTime(?, ?, 0)

evaluatePosition(?lap, ?position, ?density) <= Get(Standings.Extrapolated, ?lap, Car, !Driver.Car, Position, ?position),						\
											   trafficDensity(?lap, ?density)

trafficDensity(?lap, ?density) <= Get(Standings.Extrapolated, ?lap, Car, !Driver.Car, Laps, ?from),												\
								  ?to = ?from + !Session.Settings.Strategy.Traffic.Considered,													\
								  trafficDensity(?lap, !Car.Count, ?from, ?to, ?density)

trafficDensity(?, ?car, ?, ?, 0) <= =<(?car, 0), !
trafficDensity(?lap, ?car, ?from, ?to, ?count) <= Get(Standings.Extrapolated, ?lap, Car, ?car, Laps, ?laps), ?laps > ?from, ?laps < ?to, !,		\
												  ?nextCar = ?car - 1, trafficDensity(?lap, ?nextCar, ?from, ?to, ?temp), ?count = ?temp + 1
trafficDensity(?lap, ?car, ?from, ?to, ?count) <= ?nextCar = ?car - 1, trafficDensity(?lap, ?nextCar, ?from, ?to, ?count)

bestPitstopLap([?cLap | ?laps], [?cPosition | ?positions], [?cTrafficDensity | ?trafficDensities], ?lap) <=										\
		bestPitstopLap(?laps, ?positions, ?trafficDensities, ?cLap, ?cPosition, ?cTrafficDensity, ?lap)

bestPitstopLap([], [], [], ?candidateLap, ?, ?, ?candidateLap)
bestPitstopLap([?cLap | ?laps], [?cPosition | ?positions], [?cDensity | ?densities], ?bestLap, ?bestPosition, ?bestDensity, ?lap) <=			\
		?cPosition < ?bestPosition, !, bestPitstopLap(?laps, ?positions, ?densities, ?cLap, ?cPosition, ?cDensity, ?lap)
bestPitstopLap([?cLap | ?laps], [?cPosition | ?positions], [?cDensity | ?densities], ?bestLap, ?bestPosition, ?bestDensity, ?lap) <=			\
		?cPosition = ?bestPosition, ?cDensity < ?bestDensity, !,																				\
		bestPitstopLap(?laps, ?positions, ?densities, ?cLap, ?cPosition, ?cDensity, ?lap)
bestPitstopLap([?cLap | ?laps], [?cPosition | ?positions], [?cDensity | ?densities], ?bestLap, ?bestPosition, ?bestDensity, ?lap) <=			\
		?cPosition = ?bestPosition, ?cDensity = ?bestDensity, !,																				\
		bestPitstopLap(?laps, ?positions, ?densities, ?cLap, ?cPosition, ?cDensity, ?lap)
bestPitstopLap([? | ?laps], [? | ?positions], [? | ?densities], ?bestLap, ?bestPosition, ?bestDensity, ?lap) <=									\
		bestPitstopLap(?laps, ?positions, ?densities, ?bestLap, ?bestPosition, ?bestDensity, ?lap)


;;;-------------------------------------------------------------------------;;;
;;;                             Pitstop Performed                           ;;;
;;;                                                                         ;;;
;;; After a pitstop has been performed, the pitstop plan is copied to the   ;;;
;;; history memory as a reference for future calculations.                  ;;;
;;;-------------------------------------------------------------------------;;;

[?Pitstop.Lap] => (Prove: updatePitstopLap, ?Pitstop.Lap)
priority: -20, [?Pitstop.Lap] => (Clear: Pitstop.Lap)

updatePitstopLap(?lap) <= Get(Pitstop.Last, ?last), Get(Pitstop, ?last, Lap, ?lap), !
updatePitstopLap(?lap) <= Get(Pitstop.Last, ?last), ?next = ?last + 1, Set(Pitstop, ?next, Lap, ?lap), Set(Pitstop.Last, ?next), !
updatePitstopLap(?lap) <= Set(Pitstop.1.Lap, ?lap), Set(Pitstop.Last, 1)


;;;-------------------------------------------------------------------------;;;
;;;                      Standings Information Retrieval                    ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Standings Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                        Tyre Information Retrieval                       ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Tyre Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                         Lap Information Retrieval                       ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Lap Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                       Pitstop Information Retrieval                     ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Pitstop Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                       Weather Information Retrieval                     ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Weather Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                   Session & Stint Information Retrieval                 ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Session Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                         Statistical Computations                        ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Statistical Computations.rules


;;;-------------------------------------------------------------------------;;;
;;;                               Utilities                                 ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Utilities.rules