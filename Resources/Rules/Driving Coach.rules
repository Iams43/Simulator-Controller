;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Driving Coach Rules             ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2024) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                             Global Trigger                              ;;;
;;;                                                                         ;;;
;;; Entry points for the surrounding script code. Typically, the rule       ;;;
;;; will be triggered by setting a target fact and call "produce()" on the  ;;;
;;; KnowledgeBase instance.                                                 ;;;
;;;-------------------------------------------------------------------------;;;

;;;-------------------------------------------------------------------------;;;
;;; Each lap, after all the lap data has been copied to the knowledge base, ;;;
;;; all target data for race positions, lap times and strategy will be      ;;;
;;; recomputed upon the data of the last laps, and so on.                   ;;;
;;;-------------------------------------------------------------------------;;;

; [?Lap] => ...


;;;-------------------------------------------------------------------------;;;
;;; This fact is set for each invocation of the rule engine. It can be used ;;;
;;; to start periodic tasks.                                                ;;;
;;;-------------------------------------------------------------------------;;;

; [?Update] => ...

priority: -20, [?Update] => (Clear: Update)


;;;-------------------------------------------------------------------------;;;
;;; The race positions and lap times will be recomputed.                    ;;;
;;;-------------------------------------------------------------------------;;;

; [?Standings] => ...


;;;-------------------------------------------------------------------------;;;
;;; "Pitstop.Lap" must be set to the lap number, where the pitstop has been ;;;
;;; done.                                                                   ;;;
;;;-------------------------------------------------------------------------;;;

; [?Pitstop.Lap] => ...


;;;-------------------------------------------------------------------------;;;
;;; "Cleanup" can be set to remove unnecessary knowledge from the memory.   ;;;
;;; Set "Cleanup" to "Laps" to remove all recent laps, that are outside the ;;;
;;; the statistical window. This is done automatically after a pitstop.     ;;;
;;; You can also set it to "Standings", which will remove all standings     ;;;
;;; from the memory, which are "older" than									;;;
;;; "Session.Settings.Lap.History.Considered".								;;;
;;;-------------------------------------------------------------------------;;;

; [?Cleanup] => ...


;;;-------------------------------------------------------------------------;;;
;;;                          Lap & Fuel Calculation                         ;;;
;;;                                                                         ;;;
;;; Updates the remaining laps with the current amount of fuel and the      ;;;
;;; remaining stint time for the current driver.                            ;;;
;;;-------------------------------------------------------------------------;;;

priority: 20, [?Lap] => (ProveAll: updateRemainingLaps(?Lap))

updateRemainingLaps(?lap) <= remainingStintLaps(?lap, Fuel, ?fuelLaps), Set(Lap.Remaining.Fuel, ?fuelLaps)
updateRemainingLaps(?lap) <= remainingStintLaps(?lap, Driver, ?driverLaps), Set(Lap.Remaining.Stint, ?driverLaps)
updateRemainingLaps(?lap) <= remainingSessionLaps(?lap, ?sessionLaps), Set(Lap.Remaining.Session, ?sessionLaps)
updateRemainingLaps(?lap) <= !Lap.Remaining.Stint > !Lap.Remaining.Fuel, Set(Lap.Remaining.Stint, !Lap.Remaining.Fuel)
updateRemainingLaps(?lap) <= !Lap.Remaining.Stint > !Lap.Remaining.Session, Set(Lap.Remaining.Stint, !Lap.Remaining.Session)

priority: 20, {All: [?Lap.Remaining.Stint <= ?Lap.Remaining.Fuel],
					[?Lap.Remaining.Stint <= ?Lap.Remaining.Session]} => (Set: Lap.Remaining, ?Lap.Remaining.Stint)
priority: 20, {All: [?Lap.Remaining.Session <= ?Lap.Remaining.Fuel],
					[?Lap.Remaining.Session <= ?Lap.Remaining.Stint]} => (Set: Lap.Remaining, ?Lap.Remaining.Session)
priority: 20, {All: [?Lap.Remaining.Fuel <= ?Lap.Remaining.Stint],
					[?Lap.Remaining.Fuel <= ?Lap.Remaining.Session]} => (Set: Lap.Remaining, ?Lap.Remaining.Fuel)


;;;-------------------------------------------------------------------------;;;
;;;                      Position & Lap Delta Calculation                   ;;;
;;;                                                                         ;;;
;;; Updates the gaps and lap time deltas for various cars each lap.         ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Standings Computations.rules


;;;-------------------------------------------------------------------------;;;
;;;                             Pitstop Performed                           ;;;
;;;                                                                         ;;;
;;; After a pitstop has been performed, the pitstop plan is copied to the   ;;;
;;; history memory as a reference for future calculations.                  ;;;
;;;-------------------------------------------------------------------------;;;

[?Pitstop.Lap] => (Prove: updatePitstopLap(?Pitstop.Lap))
priority: -20, [?Pitstop.Lap] => (Clear: Pitstop.Lap)

updatePitstopLap(?lap) <= Get(Pitstop.Last, ?last), Get(Pitstop, ?last, Lap, ?lap), !
updatePitstopLap(?lap) <= Get(Pitstop.Last, ?last), ?next = ?last + 1, Set(Pitstop, ?next, Lap, ?lap), Set(Pitstop.Last, ?next), !
updatePitstopLap(?lap) <= Set(Pitstop.1.Lap, ?lap), Set(Pitstop.Last, 1)


;;;-------------------------------------------------------------------------;;;
;;;                            Telemetry Coaching                           ;;;
;;;                                                                         ;;;
;;; These rules compute a number of recommendations and instructions for an ;;;
;;; upcoming corner baesd on data about the last lap and possibly a         ;;;
;;; reference lap.                                                          ;;;
;;;-------------------------------------------------------------------------;;;

{All: [?Lap.Corner.Entry.Braking.Start < ?Reference.Corner.Entry.Braking.Start]} =>
		(Prove: addDriverInput(LateBraking))

{All: [?Lap.Corner.Entry.Braking.Start > ?Reference.Corner.Entry.Braking.Start]} =>
		(Prove: addDriverInput(EarlyBraking))

{All: [?Lap.Corner.Entry.Braking.Pressure < ?Reference.Corner.Entry.Braking.Pressure]} =>
		(Prove: addDriverInput(SoftBraking))

{All: [?Lap.Corner.Entry.Braking.Pressure > ?Reference.Corner.Entry.Braking.Pressure]} =>
		(Prove: addDriverInput(HardBraking))

{All: [?Lap.Corner.Exit.Accelerating.Start < ?Reference.Corner.Exit.Accelerating.Start]} =>
		(Prove: addDriverInput(EarlyAccelerating))

{All: [?Lap.Corner.Exit.Accelerating.Start > ?Reference.Corner.Exit.Accelerating.Start]} =>
		(Prove: addDriverInput(LateAccelerating))

{All: [?Lap.Corner.Entry.Braking.Distance > ?Reference.Corner.Entry.Braking.Distance],
	  [?Lap.Corner.Entry.ABSActivations > ?Reference.Corner.Entry.ABSActivations]} =>
		(Prove: addDriverInput(OverBraking))

{All: [?Lap.Corner.Entry.Braking.Distance > ?Reference.Corner.Entry.Braking.Distance],
	  [?Lap.Corner.Entry.ABSActivations =< ?Reference.Corner.Entry.ABSActivations]} =>
		(Prove: addDriverInput(UnderBraking))

{All: {Prove: worsePerformance()},
	  [?Lap.Corner.Entry.Braking.Start < ?Reference.Corner.Entry.Braking.Start],
	  {Any: [?Lap.Corner.Steering.Smoothness < ?Reference.Corner.Steering.Smoothness],
			[?Lap.Corner.Brake.Smoothness < ?Reference.Corner.Brake.Smoothness],
			[?Lap.Corner.Exit.ABSActivations > ?Reference.Corner.ABSActivations]}} =>
		(Prove: addDriverInput(OverPushing))

{All: {Prove: worsePerformance()},
	  [?Lap.Corner.Entry.Braking.Start > ?Reference.Corner.Entry.Braking.Start],
	  {Any: [?Lap.Corner.Steering.Smoothness > ?Reference.Corner.Steering.Smoothness],
			[?Lap.Corner.Brake.Smoothness > ?Reference.Corner.Brake.Smoothness],
			[?Lap.Corner.Exit.ABSActivations < ?Reference.Corner.ABSActivations]}} =>
		(Prove: addDriverInput(UnderPushing))

{All: {Prove: worsePerformance()},
	  [?Lap.Corner.Exit.Accelerating.Start < ?Reference.Corner.Exit.Accelerating.Start],
	  {Any: [?Lap.Corner.Exit.TCActivations > ?Reference.Corner.Exit.TCActivations],
			[?Lap.Corner.Throttle.Smoothness < ?Reference.Corner.Throttle.Smoothness],
			[?Lap.Corner.Steering.Smoothness < ?Reference.Corner.Steering.Smoothness]}} =>
		(Prove: addDriverInput(OverPushing))

{All: {Prove: worsePerformance()},
	  [?Lap.Corner.Exit.Accelerating.Start > ?Reference.Corner.Exit.Accelerating.Start],
	  {Any: [?Lap.Corner.Exit.TCActivations < ?Reference.Corner.Exit.TCActivations],
			[?Lap.Corner.Throttle.Smoothness > ?Reference.Corner.Throttle.Smoothness],
			[?Lap.Corner.Steering.Smoothness > ?Reference.Corner.Steering.Smoothness]}} =>
		(Prove: addDriverInput(UnderPushing))

addDriverInput(?input) <= Set(DriverInputs, !DriverInputs.Count, ?input),
						  ?count = !DriverInputs.Count + 1, Set(DriverInputs.Count, ?count)

betterPerformance() <= ?lapTime = !Lap.Corner.Time + !Lap.Following.Time,
					   ?referenceTime = !Reference.Corner.Time + !Reference.Following.Time,
					   ?lapTime < ?referenceTime
					   
worsePerformance() <= betterPerformance(), !, fail
worsePerformance()
										  
[?Coaching.Cleanup] => (ProveAll: clearTelemetry()), (Prove: clearDriverInputs()),
					   (Clear: Coaching.Cleanup)

clearDriverInputs() <= clearDriverInputs(!DriverInputs.Count)

clearDriverInputs(0) <= !
clearDriverInputs(?index) <= Clear(DriverInputs, ?index), ?newIndex = ?index - 1, clearDriverInputs(?newIndex)

clearTelemetry() <= clearTelemetry(Lap.Corner)
clearTelemetry() <= clearTelemetry(Lap.Straight)
clearTelemetry() <= clearTelemetry(Reference.Corner)
clearTelemetry() <= clearTelemetry(Reference.Straight)

clearTelemetry(Lap.Corner) <= Clear(Lap.Corner.Nr), Clear(Lap.Corner.Time), Clear(Lap.Corner.Length),
							  Clear(Lap.Corner.Steering.Corrections), Clear(Lap.Corner.Steering.Smoothness),
							  clearTelemetry(Lap.Corner, [Entry, Apex, Exit])
clearTelemetry(Lap.Straight) <= Clear(Lap.Straight.Nr), Clear(Lap.Straight.Time), Clear(Lap.Straight.Length),
								Clear(Lap.Straight.Speed.Min), Clear(Lap.Straight.Speed.Max), Clear(Lap.Straight.Speed.Avg)

clearTelemetry(Reference.Corner) <= Clear(Reference.Corner.Nr), Clear(Reference.Corner.Time), Clear(Reference.Corner.Length),
									Clear(Reference.Corner.Steering.Corrections), Clear(Reference.Corner.Steering.Smoothness),
									clearTelemetry(Reference.Corner, [Entry, Apex, Exit])
clearTelemetry(Reference.Straight) <= Clear(Reference.Straight.Nr),
									  Clear(Reference.Straight.Time),
									  Clear(Reference.Straight.Length),
									  Clear(Reference.Straight.Speed.Min),
									  Clear(Reference.Straight.Speed.Max),
									  Clear(Reference.Straight.Speed.Avg)

clearTelemetry(?type, [])
clearTelemetry(?type, [?phase | ?phases]) <= clearTelemetry(?type, ?phase), clearTelemetry(?type, ?phases)

clearTelemetry(?type, Entry) <= Clear(?type, Entry.Time),
								Clear(?type, Entry.Braking.Start),
								Clear(?type, Entry.Braking.Length),
								Clear(?type, Entry.Braking.Pressure),
								Clear(?type, Entry.Braking.Rampup),
								Clear(?type, Entry.Brake.Corrections),
								Clear(?type, Entry.Brake.Smoothness),
								Clear(?type, Entry.ABSActivations), !

clearTelemetry(?type, Apex) <= Clear(?type, Apex.Time),
							   Clear(?type, Apex.Rolling.Start),
							   Clear(?type, Apex.Rolling.Length),
							   Clear(?type, Apex.Acceleration.Lateral),
							   Clear(?type, Apex.Gear),
							   Clear(?type, Apex.RPM),
							   Clear(?type, Apex.Speed), !

clearTelemetry(?type, Exit) <= Clear(?type, Exit.Time),
							   Clear(?type, Exit.Accelerating.Start),
							   Clear(?type, Exit.Accelerating.Length),
							   Clear(?type, Exit.Acceleration.Lateral),
							   Clear(?type, Exit.Gear),
							   Clear(?type, Exit.RPM),
							   Clear(?type, Exit.Speed),
							   Clear(?type, Exit.Throttle.Corrections),
							   Clear(?type, Exit.Throttle.Smoothness),
							   Clear(?type, Exit.TCActivations), !


;;;-------------------------------------------------------------------------;;;
;;;                              Memory Cleanup                             ;;;
;;;                                                                         ;;;
;;; These rules remove knowledge that is no longer needed.					;;;
;;;-------------------------------------------------------------------------;;;

[?Lap] => (Set: Cleanup, Laps)

priority: -40, [?Cleanup = Laps] => (Prove: clearLaps()), (Set: Cleanup, Standings)

clearLaps() <= ?lap = !Lap - 1, ?clearLap = ?lap - !Session.Settings.Lap.History.Considered, clearLaps(?clearLap)

clearLaps(0) <= !
clearLaps(?lap) <= ?lap < 0, !
clearLaps(?lap) <= clearLap(?lap), ?pLap = ?lap - 1, clearLaps(?pLap)

priority: -40, [?Cleanup = Standings] => (Prove: clearStandings()), (Clear: Cleanup)

clearStandings() <= ?lap = !Lap - 1, ?clearLap = ?lap - !Session.Settings.Lap.History.Considered, clearStandings(?clearLap)

clearStandings(0) <= !
clearStandings(?lap) <= ?lap < 0, !
clearStandings(?lap) <= clearStanding(?lap), ?pLap = ?lap - 1, clearStandings(?pLap)

clearStanding(?lap) <= Append(Standings.Lap., ?lap, .Car.Count, ?fact), fact?(?fact), !, Get(?fact, ?carCount), Clear(?fact),
					   Clear(Standings.Lap, ?lap, Time), Clear(Standings.Lap, ?lap, Position), Clear(Standings.Lap, ?lap, Weather),
					   clearStanding(?lap, ?carCount), clearExtrapolatedLap(?lap, ?carCount)
clearStanding(?)

clearStanding(?, 0) <= !
clearStanding(?, ?car) <= ?car < 0, !
clearStanding(?lap, ?car) <= clearStanding(?lap, ?car, [Nr, ID, Delta, Laps, Time, Time.Average,
														Position, Driver.Forname, Driver.Surname, Driver.Nickname, Driver.Category]),
							 ?pCar = ?car - 1, clearStanding(?lap, ?pCar)

clearStanding(?, ?, []) <= !
clearStanding(?lap, ?car, [?name | ?names]) <= Clear(Standings.Lap, ?lap, Car, ?car, ?name),
											   clearStanding(?lap, ?car, ?names)


;;;-------------------------------------------------------------------------;;;
;;;                         Car Information Retrieval                       ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Car Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                        Tyre Information Retrieval                       ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Tyre Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                         Lap Information Retrieval                       ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Lap Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                       Pitstop Information Retrieval                     ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Pitstop Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                       Weather Information Retrieval                     ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Weather Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                   Session & Stint Information Retrieval                 ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Session Information Retrieval.rules


;;;-------------------------------------------------------------------------;;;
;;;                         Statistical Computations                        ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Statistical Computations.rules


;;;-------------------------------------------------------------------------;;;
;;;                               Utilities                                 ;;;
;;;-------------------------------------------------------------------------;;;

#Include %kResourcesDirectory%Rules\Utilities.rules