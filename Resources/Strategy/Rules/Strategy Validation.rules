;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;   Modular Simulator Controller System - Strategy Validation Rules       ;;;
;;;                                                                         ;;;
;;;   Author:     Oliver Juwig (TheBigO)                                    ;;;
;;;   License:    (2022) Creative Commons - BY-NC-SA                        ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;-------------------------------------------------------------------------;;;
;;;                            Public Rules Section                         ;;;
;;;-------------------------------------------------------------------------;;;

totalFuel(?fuelAmount, ?numRefuels) <= startFuel(?startFuel), pitstopFuel(?refuels, ?numRefuels), ?fuelAmount = ?startFuel + ?refuels

startFuel(?startFuel) <= setup(?startFuel, ?, ?)

pitstopFuel(?refuelAmount, ?numRefuels) <= pitstopRefuels(1, ?refuelAmount, ?numRefuels)
		
startTyreSet(?tyreCompound, ?tyreCompoundColor) <= setup(?, ?tyreCompound, ?tyreCompoundColor)

pitstopTyreSets(?tyreSets) <= pitstopTyreSets(1, ?tyreSets)

refuels(?refuels) <= totalFuel(?, ?refuels)

tyreSets([[?tyreCompound | ?tyreCompoundColor] | ?tyreSets]) <= startTyreSet(?tyreCompound, ?tyreCompoundColor), pitstopTyreSets(?tyreSets)

tyreCompounds([], ?, [])
tyreCompounds([[?compound | ?compoundColor] | ?tyreSets], ?compound, [?compoundColor | ?result]) <=
		tyreCompounds(?tyreSets, ?compound, ?result)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;                             Internal Rules                              ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

pitstopRefuels(?pitstop, ?refuelAmount, ?numRefuels) <=
		pitstop(?pitstop, ?pitstopRefuelAmount, ?, ?), ?pitstopRefuelAmount > 0, !,
		?nextPitstop = ?pitstop + 1, pitstopRefuels(?nextPitstop, ?nextRefuelAmount, ?nextNumRefuels),
		?refuelAmount = ?pitstopRefuelAmount + ?nextRefuelAmount, ?numRefuels = ?nextNumRefuels + 1
pitstopRefuels(?, 0, 0)

pitstopTyreSets(?pitstop, ?tyreSets) <= pitstop(?pitstop, ?, false, ?), !, ?nextPitstop = ?pitstop + 1, pitstopTyreSets(?nextPitstop, ?tyreSets)
pitstopTyreSets(?pitstop, [[?tyreCompound | ?tyreCompoundColor] | ?tyreSets]) <=
		pitstop(?pitstop, ?, ?tyreCompound, ?tyreCompoundColor), !, ?nextPitstop = ?pitstop +1, pitstopTyreSets(?nextPitstop, ?tyreSets)
pitstopTyreSets(?, [])


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;                            Collection Rules                             ;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

any?(?value, [?value | ?])
any?(?value, [? | ?tail]) <= any?(?value, ?tail)

all?(?value, [?value])
all?(?value, [?value | ?tail]) <= all?(?value, ?tail)

none?(?value, [])
none?(?value, [?value | ?]) <= !, fail
none?(?value, [? | ?tail]) <= none?(?value, ?tail)

one?(?value, []) <= fail
one?(?value, [?value | ?tail]) <= !, none?(?value, ?tail)
one?(?value, [? | ?tail]) <= one?(?value, ?tail)
				
length([], 0)
length([?h | ?t], ?length) <= length(?t, ?tLength), ?length = ?tLength + 1

unique([], [])
unique([?value | ?values], [?value | ?result]) <= none?(?value, ?values), !, unique(?values, ?result)
unique([?value | ?values], ?result) <= unique(?values, ?result)

reverse([], [])
reverse([?h | ?t], ?rev) <= reverse(?t, ?rt), concat(?rt, [?h], ?rev)

concat([], ?l, ?l)
concat([?h | ?t], ?l, [?h | ?r]) <= concat(?t, ?l, ?r)

remove([], ?, [])
remove([?h | ?t], ?h, ?result) <= !, remove(?t, ?h, ?result)
remove([?h | ?t], ?x, [?h | ?result]) <= remove(?t, ?x, ?result)